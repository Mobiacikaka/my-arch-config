let g:airline#extensions#tabline#enabled = 0
let g:airline#extensions#tabline#left_sep = ''
let g:airline#extensions#tabline#left_alt_sep = ''
let g:airline#extensions#tabline#ignore_bufadd_pat =
  \ 'gundo|undotree|vimfiler|tagbar|nerd_tree|startify|!'

let g:airline#extensions#tabline#tabtitle_formatter = 'AirLineTabTitleFormatter'
func! AirLineTabTitleFormatter(n)
    let buflist = tabpagebuflist(a:n)
    let winnr = tabpagewinnr(a:n)
    let bufnr = buflist[winnr - 1]
    let name = bufname(bufnr)

    if empty(name)
        if getqflist({'qfbufnr' : 0}).qfbufnr == bufnr
            let title = airline#extensions#tabline#formatters#default#wrap_name(bufnr, '[Quickfix List]')
        elseif win_getid(tabpagewinnr(a:n), a:n) && getloclist(win_getid(tabpagewinnr(a:n), a:n), {'qfbufnr' : 0}).qfbufnr == bufnr
            let title = airline#extensions#tabline#formatters#default#wrap_name(bufnr, '[Location List]')
        else
            let title = airline#extensions#tabline#formatters#default#wrap_name(bufnr, '[No Name]')
        endif
    else
        if name =~ 't//'
            " Neovim terminal
            let tail = substitute(name, '\(term:\)//.*:\(.*\)', '\1 \2', '')
        else
            let tail = fnamemodify(name, ':s?/\+$??:t')
        endif
        let title = airline#extensions#tabline#formatters#default#wrap_name(bufnr, tail)
    endif

    return title != '' ? title : airline#extensions#tabline#formatters#default#format(bufnr, buflist)
endfunc
